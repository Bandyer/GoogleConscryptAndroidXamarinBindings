apply plugin : 'java'

def conscryptAndroid = '2.2.1'

repositories {
    mavenLocal()
    maven { url 'https://maven.google.com' }
    jcenter()
    mavenCentral()
}

configurations {
    aar
    docs
}

configurations.all {
    transitive = false
}

dependencies {
    aar group: 'org.conscrypt', name:'conscrypt-android', version: "$conscryptAndroid"
    docs group: 'org.conscrypt', name:'conscrypt-android', classifier: 'javadoc', version: "$conscryptAndroid"
}

clean() {
    delete 'Javadoc', fileTree('Jars') { include '**/*.aar' }
}

task copyAndRenameAar(type: Copy) {
    from file(configurations.aar.singleFile)
    into 'Jars'
    rename { 'conscrypt-android.aar' }
}

task copyAndExtractJavaDoc(type: Copy) {
    from zipTree(configurations.docs.singleFile)
    into "Javadoc"
}

task allDeps(type: DependencyReportTask) {
    configurations = [project.configurations.aar, project.configurations.docs]
}

task fetch(){
    dependsOn clean
    dependsOn allDeps
    dependsOn copyAndRenameAar
    dependsOn copyAndExtractJavaDoc
}